
neuron iaf_psc_exp_active_dendrite_resetting92ab47ce14a84d39bae291482ee6b349:
    state:
        V_m mV = 0 mV     # membrane potential
        t_dAP ms = 0 ms   # dendritic action potential timer
        I_dAP pA = 0 pA   # dendritic action potential current magnitude
        enable_I_syn real = 1.   # set to 1 to allow synaptic currents to
                                 # contribute to V_m integration, 0 otherwise

    equations:
        # alpha shaped postsynaptic current kernel
        kernel syn_kernel = (e / tau_syn) * t * exp(-t / tau_syn)
        recordable inline I_syn pA = convolve(syn_kernel, spikes_in) * pA
        V_m' = -(V_m - E_L) / tau_m + (enable_I_syn * I_syn + I_dAP + I_e) / C_m

    parameters:
        C_m pF = 250 pF          # capacity of the membrane
        tau_m ms = 20 ms         # membrane time constant
        tau_syn ms = 10 ms       # time constant of synaptic current
        V_th mV = 25 mV          # action potential threshold
        V_reset mV = 0 mV        # reset voltage
        I_e    pA = 0 pA         # external current
        E_L    mV = 0 mV         # resting potential

        # dendritic action potential
        I_th pA = 100 pA         # current-threshold for a dendritic action potential
        I_dAP_peak pA = 450 pA   # current clamp value for I_dAP during a dendritic action potential
        T_dAP ms = 30 ms         # time window over which the dendritic current clamp is active

    input:
        spikes_in <- spike

    output:
        spike

    update:
        # solve ODEs
        integrate_odes()

        if t_dAP > 0 ms:
            t_dAP -= resolution()
            if t_dAP <= 0 ms:
                I_dAP = 0 pA
                t_dAP = 0 ms
                # reset and re-enable synaptic integration
                I_syn = 0 pA
                I_syn' = 0 * s**-1
                enable_I_syn = 1.

        if I_syn > I_th:
            # current-threshold, emit a dendritic action potential
            t_dAP = T_dAP
            I_dAP = I_dAP_peak
            # temporarily pause synaptic integration
            enable_I_syn = 0.
            
        # emit somatic action potential
        if V_m > V_th:
            emit_spike()
            V_m = V_reset

